plugins {
    id 'java'
    id 'application'
    id 'checkstyle'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.10.0'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.10.0'
}

checkstyle {
    toolVersion = '10.12.5'
    ignoreFailures = true
    configDirectory = layout.projectDirectory.dir('config/checkstyle')
}

test {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"

        showExceptions true
        exceptionFormat "full"
        showCauses true
        showStackTraces true
        showStandardStreams = false
    }
}

application {
    mainClass.set("monday.Monday")
}

shadowJar {
    archiveBaseName = "monday"
    archiveClassifier = null
}

run {
    standardInput = System.in
}

javadoc {
    options.memberLevel = JavadocMemberLevel.PUBLIC
}

tasks.register('runTextUiTest', Exec) {
    if (System.getProperty('os.name').toLowerCase().contains('windows')) {
        executable 'cmd'
        args '/c', 'text-ui-test\\runtest.bat'
    } else {
        executable 'bash'
        args 'text-ui-test/runtest.sh'
    }
}
